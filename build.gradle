buildscript {
    ext {
        springBootVersion = '2.5.1'
        managementVersion = '1.0.11.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$managementVersion"
    }
}

allprojects {
    group = 'learn.jpa'
    version = ''
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    dependencies {
        annotationProcessor(
                'org.projectlombok:lombok',
                'org.springframework.boot:spring-boot-configuration-processor'
        )
        compileOnly(
                'org.projectlombok:lombok'
        )
        runtimeOnly(
                'com.h2database:h2'
        )
        implementation(
                'org.springframework.boot:spring-boot-starter-web',
                'org.springframework.boot:spring-boot-starter-validation',
                'org.modelmapper:modelmapper:2.4.4',
                'commons-io:commons-io:2.8.0'
        )
        testImplementation(
                'org.springframework.boot:spring-boot-starter-test',
                'org.springframework.restdocs:spring-restdocs-mockmvc'
        )
    }

    test {
        useJUnitPlatform()
    }
}

['jpa', 'data-jpa'].each {
    project(it) {
        bootJar.enabled(false)
        jar.enabled(true)
    }
}
